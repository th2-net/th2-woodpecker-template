plugins {
    id 'application'
    id 'com.palantir.docker' version '0.25.0'
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    id "com.github.harbby.gradle.serviceloader" version '1.1.8'
    id "org.owasp.dependencycheck" version "8.1.2"
}

group = 'com.exactpro.th2'
version = release_version

sourceCompatibility = 11
targetCompatibility = 11

compileKotlin.kotlinOptions.jvmTarget = "11"
compileTestKotlin.kotlinOptions.jvmTarget = "11"

repositories {
    mavenCentral()

    maven {
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }

    mavenLocal()

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    api platform('com.exactpro.th2:bom:4.2.0')

    implementation 'com.exactpro.th2:common:5.2.0-separate-executor-4807123825-9449978-SNAPSHOT'
    implementation 'com.exactpro.th2:woodpecker:2.0.0-demo-router-4817949420-60fc332-SNAPSHOT'
    implementation 'com.exactpro.th2:grpc-woodpecker:2.0.0-dev'

    implementation 'org.slf4j:slf4j-api'

    implementation 'io.netty:netty-buffer'

    implementation 'com.google.protobuf:protobuf-java'

    implementation 'commons-io:commons-io'
    implementation 'org.apache.commons:commons-lang3'

    implementation 'io.prometheus:simpleclient'

    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: '3.0.0'

    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5', version: kotlin_version
}

test {
    useJUnitPlatform()
}

application {
    mainClassName 'com.exactpro.th2.woodpecker.Main'
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

serviceLoader {
    serviceInterface 'com.exactpro.th2.woodpecker.api.IMessageGenerator'
    serviceInterface 'com.exactpro.th2.woodpecker.api.IMessageGeneratorFactory'
}

configurations {
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}

dependencyCheck {
    formats=['SARIF', 'JSON', 'HTML']
    failBuildOnCVSS=5

    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}